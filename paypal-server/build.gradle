description = 'paypal-server'

buildscript {
    repositories {
        mavenCentral()
    }
}

dependencies {
    implementation project(':paypal-core')
    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'

    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.12.v20180830'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.12.v20180830'
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

apply plugin: 'application'
mainClassName = "de.micromata.paypal.PayPalMain"

run() {
    doFirst {
        jvmArgs = [
                "-DapplicationHome=${rootDir}"
        ]
    }
}

apply plugin: 'distribution'

// Ugly work arround for getting the applications home dir:
applicationDefaultJvmArgs = ["-DapplicationHome=MY_APPLICATION_HOME"]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APPLICATION_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APPLICATION_HOME', '%~dp0..')
    }
}

// Builds the distribution
distributions {
    main {
        contents {
            from ("web/") {
                into 'web'
            }
        }
    }
}
